plugins {
	id 'com.android.library'
	id 'maven-publish'
}

android {
	namespace 'cz.adaptech.tesseract4android'
	compileSdk 35
	ndkVersion "27.2.12479018"

	defaultConfig {
		minSdk 21
		targetSdk 35

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		externalNativeBuild {
			cmake {
				arguments "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"
			}
		}
		ndk {
		}
	}
	externalNativeBuild {
		cmake {
			path "src/main/cpp/CMakeLists.txt"
			version '3.22.1'
		}
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	// BU KISMI EKLEYİN - Debug variant'ları atla
	variantFilter { variant ->
		if (variant.buildType.name == 'debug') {
			setIgnore(true)
		}
	}

	flavorDimensions = ["parallelization"]
	productFlavors {
		standard {
		}
		openmp {
			externalNativeBuild {
				cmake {
					cFlags "-fopenmp -static-openmp -Wno-unused-command-line-argument"
					cppFlags "-fopenmp -static-openmp -Wno-unused-command-line-argument"
				}
			}
		}
	}
	publishing {
		singleVariant("standardRelease") {
			withSourcesJar()
			withJavadocJar()
		}
		singleVariant("openmpRelease") {
			withSourcesJar()
			withJavadocJar()
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}
	buildFeatures {
		buildConfig true
	}
}

dependencies {
	implementation 'androidx.annotation:annotation:1.3.0'

	testImplementation 'junit:junit:4.13.2'
	androidTestImplementation 'androidx.test:runner:1.6.2'
	androidTestImplementation 'androidx.test:rules:1.6.1'
	androidTestImplementation 'androidx.test.ext:junit:1.2.1'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

afterEvaluate {
	publishing {
		publications {
			release(MavenPublication) {
				from components.standardRelease
				groupId = "com.github.android-video-call"
				artifactId = "tesseract4android"
				version = "4.9.4"
			}
		}
	}
}